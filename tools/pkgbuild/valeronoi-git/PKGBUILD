# Maintainer: Christian Friedrich Coors <me@ccoors.de>
pkgname=valeronoi-git
pkgver=r38.b239f2e
pkgrel=1
pkgdesc="A companion app for Valetudo for creating WiFi maps"
arch=('any')
url="https://github.com/ccoors/Valeronoi"
license=('GPL3')
depends=("qt6-base" "qt6-imageformats" "qt6-svg" "cgal")
conflicts=("valeronoi")
source=("$pkgname"::"git+https://github.com/ccoors/Valeronoi")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    mkdir -p "${srcdir}/$pkgname/build"
    cd "${srcdir}/$pkgname/build"
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
    make
}

check() {
    cd "${srcdir}/$pkgname/build"
    ./valeronoi-tests
}

package() {
    cd "${srcdir}/$pkgname/build"
    make DESTDIR="$pkgdir" install
}
